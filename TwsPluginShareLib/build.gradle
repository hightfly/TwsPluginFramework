apply plugin: 'com.android.library'

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildtools
    defaultConfig {
        minSdkVersion rootProject.ext.minSdkVersion
    }

    sourceSets.main {
        java.srcDirs = ['src/main/java']
        manifest.srcFile 'src/main/AndroidManifest.xml'
        res.srcDirs = ['src/main/res']
        jniLibs.srcDirs = ['libs']
        assets.srcDirs = ['assets']
        aidl.srcDirs = ['src/main/java']
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    aaptOptions {
        additionalParameters '--package-name',
                'com.rick.tws.sharelib',
                '--forced-package-id',
                '0x6f'
    }

    lintOptions {
        abortOnError false
    }

    packagingOptions {
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
    }

    /*task to build twspluginsharelib start*/
    def version_date = new Date().format('yyMMdd');
    def version_code = 'v1.11'
    def build_sdk_dir = 'build/Sdk'
    task makeSdk(type: Jar) {
        delete(build_sdk_dir)

        // archiveName 'twspluginsharelib' + '_' + version_code + '.' + version_date + '.jar'
        archiveName 'twspluginsharelib' + '.jar'

        from('build/intermediates/classes/release/')
        from(project.zipTree("libs/qrom_component_log.jar"))

        // zip compile jar，这样插件才可以使用宿主依赖的v4 v7
        from(project.zipTree("ziplibs/appcompat-v7-25.1.1.jar"))
        from(project.zipTree("ziplibs/support-compat-25.1.1.jar"))
        from(project.zipTree("ziplibs/support-fragment-25.1.1.jar"))
        from(project.zipTree("ziplibs/support-annotations-25.1.1.jar"))
        from(project.zipTree("ziplibs/support-core-utils-25.1.1.jar"))
        from(project.zipTree("ziplibs/support-core-ui-25.1.1.jar"))

        destinationDir = file(build_sdk_dir)

        // 如果是需要将R文件打包进去的话，需要将下面两行的代码屏蔽掉
//        exclude('**/R.class')
//        exclude('**/R\$*.class')

        // makeSDK 输出的内容
        include('com/rick/tws/**/*.class')
        include('qrom/component/**/*.class')
        include('android/support/**/*.class')
    }

    makeSdk.dependsOn(build)
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    compile rootProject.ext.dependencies.support_v4
    compile rootProject.ext.dependencies.appcompat_v7
    compile rootProject.ext.dependencies.support_design
}
